buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath files(rootProject.file('gradle-test/cutter.jar'))

        classpath gradleApi()
        classpath localGroovy()
        classpath 'org.zeroturnaround:zt-zip:1.12'
        classpath group: 'org.ow2.asm', name: 'asm', version: '6.0'
        classpath group: 'org.ow2.asm', name: 'asm-commons', version: '6.0'
        classpath group: 'org.ow2.asm', name: 'asm-tree', version: '6.0'
        classpath group: 'org.ow2.asm', name: 'asm-util', version: '6.0'
        classpath "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.2.40"
        classpath group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: '1.2.40'
    }
}


plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.10'
}
apply plugin: 'idea'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'

group 'tech.justagod'
archivesBaseName = 'cutter'
version '2.0.1-SNAPSHOT'

repositories {
    mavenCentral()
}
signing {
    sign configurations.archives
}
sourceSets {
    main {
        groovy.outputDir = (compileKotlin.destinationDir = compileJava.destinationDir)
    }
}

evaluationDependsOnChildren()

def defaultsProj = project(':Defaults')
jar.dependsOn(defaultsProj.tasks.getByPath("jar"))
jar.dependsOn(defaultsProj.tasks.sourcesJar)
jar.dependsOn(defaultsProj.tasks.javadocJar)

jar {
    archiveName = "cutter.jar"
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    into('defaults') {
        from(fileTree(defaultsProj.file("build/libs")))
    }
}

test.dependsOn(jar)

test {
    workingDir = rootProject.rootDir
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}


task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

uploadArchives.dependsOn(test)

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'Cutter'
                packaging 'jar'

                description 'Gradle plugin to simplify mods protection'
                url 'https://gitlab.com/hvalick2111/cutter'


                scm {
                    connection 'scm:git:https://gitlab.com/hvalick2111/cutter.git'
                    developerConnection 'scm:git:https://gitlab.com/hvalick2111/cutter.git'
                    url 'https://gitlab.com/hvalick2111/cutter'
                }

                licenses {
                    license {
                        name 'MIT License'
                    }
                }

                developers {
                    developer {
                        id 'JustAGod'
                        name 'Yury Barinov'
                        email 'hvalick2111@gmail.com'
                    }
                }
            }
        }
    }
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'org.zeroturnaround:zt-zip:1.12'
    compile group: 'org.ow2.asm', name: 'asm-debug-all', version: '5.2'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.2.40"

    testCompile 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
}

idea {
    module {
        excludeDirs += project.file('forge')
        excludeDirs += project.file('gradle-test')
    }
}

